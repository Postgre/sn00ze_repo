select
	so-order-no
	so-bo-suffix
	so-order-date
	order-log-date
	accountcode
	ordlog-type
	ordlog-old-info
	ordlog-line-type
	ordlog-ordered-change-value
	ordlog-ordered-tax-chg-value
	ws-net-value = ordlog-ordered-change-value - ordlog-ordered-tax-chg-value 
	ws-net-value1 = ordlog-ordered-change-value / 1.1 
	so-rep-code
	ordlog-new-info
	rep-code
	rep-description
	ws-terr = input
		string
		prompt is "Enter Store No.:"
	ws-start-date = input
		date
		prompt is "Enter Start Date:"
	ws-end-date = input
		date
		prompt is "Enter End   Date:"
	company-name
	so-territory-code
from
	sales-audit-file
	sales-order-archive
	rep-master
	system-control
where
	ordlog-type in { 1 5 6 9 10 11 12 }  
	and ordlog-line-type = "SN"  
	and order-log-date > 30-NOV-2002
	and so-order-date < ws-start-date
	and so-rep-code = rep-code  
	and so-territory-code = ws-terr 
order by so-rep-code so-order-no 
format is rpt
field total-ws-net-value like ws-net-value
field sub-tot-ws-net-value like ws-net-value
field new-rep-code pic 9
report header
	page
	set total-ws-net-value = 0
header
	print 
		company-name in col 1
		today() in col 95
		tod() in col 108
		"Pageno." in col 118
		pageno() in col 126
	print "CANCELLATION/AMENDMENT REPORT" in col 44 bold 
	print "FOR OUTSTANDING ORDERS" in col 46 bold 
	skip 1
	print "Rep" in col 1
		"Rep" in col 6
		"Order" in col 31
		"Order" in col 47
		"Customer" in col 59
		"Audit" in col 74
		"Audit" in col 86
		"Stock" in col 97
	print "Code" in col 1
		"Name" in col 6
		"No." in col 32
		"Date" in col 47
		"Code" in col 61
		"Date" in col 74
		"Type" in col 87
		"Code" in col 97
	print "=================================================================================================================================" in col 1
before so-rep-code
	set new-rep-code = 1
	set sub-tot-ws-net-value = 0

detail
	if (new-rep-code)
	print
		so-rep-code in col 1
		rep-description in col 6 pic x(20)
		no newline
	endif
	print
		so-order-no in col 29
		so-bo-suffix in col 37
		so-order-date in col 45
		order-log-date in col 72
		accountcode in col 59
		ordlog-type in col 88
		ordlog-old-info in col 96 pic x(13) no newline
	if ordlog-type in { 5 6 9 10 11 } 
		print
		ws-net-value in col 114 pic zzz,zz9.99-
	elseif ordlog-type in { 1 } 
		if ordlog-new-info != " "
			print
			ws-net-value in col 114 pic zzz,zz9.99-
		endif
	elseif ordlog-type in { 12 } 
		print
		ws-net-value1 in col 114 pic zzz,zz9.99-
	endif
	if ordlog-type in { 5 6 9 10 11 } 
		set total-ws-net-value += ws-net-value
		set sub-tot-ws-net-value += ws-net-value
	elseif ordlog-type in { 1 } 
		if ordlog-new-info != " "
			set total-ws-net-value += ws-net-value
			set sub-tot-ws-net-value += ws-net-value
		endif
	elseif ordlog-type in { 12 } 
		set total-ws-net-value += ws-net-value1
		set sub-tot-ws-net-value += ws-net-value1
	endif
	set new-rep-code = 0
after so-rep-code
	print
		"-----------" in col 114
	print
		"Total for " in col 80 bold
		rep-description in col 90 pic x(20) bold
		sub-tot-ws-net-value in col 114 pic zzz,zz9.99- bold 
	skip
report footer
	print
		"-----------" in col 114
	print
		total-ws-net-value in col 114 pic zzz,zz9.99-
	skip 2
	print "Audit Types:" in col 54 underlined
	print "Report Name : cancledit.enq" in col 5
		"1 - Item price changed" in col 55
		"10 - Item added" in col 80
	print "Store No.   :" in col 5
		ws-terr in col 19 pic x(4)
		"5 - Quantity changed" in col 55
		"11 - Line discount changed" in col 80
	print "Start Date  :" in col 5
		ws-start-date in col 19
		"6 - Cancelled order" in col 55
		"12 - Order discount changed" in col 80
	print "End   Date  :" in col 5
		ws-end-date in col 19
		"9 - Item removed" in col 55
