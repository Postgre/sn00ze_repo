select
distinct
	bps-gl-bank-account
	bps-record-type
	bps-date
	bps-batch-code
	bps-accountcode
	bps-payee-detail
	bps-bank-code
	bps-branch
	bps-cheque-number
	bps-amount
	bps-order-no
	batch-code
	batch-user-id
	ws-store-code = input alpha "Which store  -  o=Orange  b=Bathurst:"
	ws-start-date = input date "Enter Start Date:"
	ws-end-date = input date "Enter End Date:"
from
	bank-pay-in-slip
	batch-entry-control-file
where
	bps-batch-code = batch-code 
	and substring (batch-user-id 1 1) = ws-store-code 
	and bps-amount != 0 
	and bps-date between ws-start-date and ws-end-date 
order by bps-record-type bps-date 
format is rpt
field new-bps-gl-bank-account pic 9
field new-bps-record-type pic 9
field total-bps-amount like bps-amount
field sub-tot-bps-amount like bps-amount
report header
	page
	set total-bps-amount = 0
header
	print 
		"rjbbanking" in col 1
		"Banking Sheet Audit Report for" in col 34 no newline
			if ws-store-code = 'o'
				print "Orange" in col 66 bold no newline
			elseif ws-store-code = 'b'	
				print "Bathurst" in col 66 bold no newline
			endif
	print
		today() in col 102
		"Page No." in col 116
		pageno() in col 124
	print "From" in col 34
		ws-start-date in col 39 bold 
		"to" in col 51
		ws-end-date in col 54 bold 
	skip 2
	print
		"Date" in col 3
		"Account" in col 14
		"Name" in col 30
		"Batch" in col 57
		"Bank" in col 64
		"Branch" in col 70
		"Chq #" in col 95
		"Order #" in col 105
		"Amount" in col 119
	print "=================================================================================================================================" in col 1
before bps-gl-bank-account
	set new-bps-gl-bank-account = 1
before bps-record-type
	set new-bps-record-type = 1
	set sub-tot-bps-amount = 0

detail
	if (new-bps-gl-bank-account)
//		print bps-gl-bank-account in col 2
	endif
skip 1
	if (new-bps-record-type)
//		print bps-record-type in col 1 bold no newline
			if bps-record-type = 'B'
				print 'Payment type:' in col 1 no newline
				print 'BANKCARD' in col 15 bold
			elseif
			bps-record-type = 'H'
				print 'Payment type:' in col 1 no newline
				print 'CASH' in col 15 bold
			elseif
			bps-record-type = 'M'
				print 'Payment type:' in col 1 no newline
				print 'MASTERCARD' in col 15 bold
			elseif
			bps-record-type = 'Q'
				print 'Payment type:' in col 1 no newline
				print 'CHEQUE' in col 15 bold
			elseif
			bps-record-type = 'T'
				print 'Payment type:' in col 1 no newline
				print 'TRANSFER' in col 15 bold
			elseif
			bps-record-type = 'V'
				print 'Payment type:' in col 1 no newline
				print 'VISA CARD' in col 15 bold
			endif
	endif
	print bps-date in col 1 no newline
	print bps-accountcode in col 14 no newline
	print bps-payee-detail in col 30 pic x(25) no newline
	print bps-batch-code in col 57 no newline
	print bps-bank-code in col 64 no newline
	print bps-branch in col 70 no newline
	print bps-cheque-number in col 93 no newline
	print bps-order-no in col 104 no newline
	print bps-amount in col 115 no newline
	skip
	set new-bps-gl-bank-account = 0
	set new-bps-record-type = 0
	set total-bps-amount += bps-amount
	set sub-tot-bps-amount += bps-amount
after bps-gl-bank-account
after bps-record-type
	print
		"---------------" in col 115
	print
		"Total" in col 110 bold 
		sub-tot-bps-amount in col 115 bold 
	skip
report footer
	print
		"---------------" in col 115 bold 
	print
		"Bank Total" in col 105 bold 
		total-bps-amount in col 115 bold 
