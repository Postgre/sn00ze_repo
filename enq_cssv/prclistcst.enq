




select
	stock-code
	stk-sort-analysis-code
	stk-condition-code
	stk-user-group
	stk-description
	stk-desc-line-2
	stk-pack-desc
	stk-sales-tax-paid-rate
	sup-last-buy-price
	cre-accountcode
	cr-shortname
	cr-type
	cr-def-payment-terms
	stock-note-type
	stock-note
	accountcode
	na-type
	na-name
	na-company
	na-street
	na-suburb
	na-country
	postcode
	na-phone
	na-fax-no
	system-table2.sys-tbl-type
	system-table2.sys-tbl-code
	system-table2.sys-description
	system-table2.sys-money-value
	ws-code-1-3 = substring (stock-code 1 3) 
	ws-cost = sup-last-buy-price / (1 + (stk-sales-tax-paid-rate / 100 ) ) 
	start-cre-type = input alpha "Enter Start Region     "
	end-cre-type = input alpha "Enter End Region       "
	start-acc = input alpha "Enter Start Accountcode"
	end-acc = input alpha "Enter End Accountcode  "
	start-user-grp = input alpha "Enter Start User Group "
	end-user-grp = input alpha "Enter End User Group   "
	sort-analysis-desc = system-table.sys-description 
	sort-analysis-money = system-table.sys-money-value
	payment-terms-desc = system-table2.sys-description 
from
	stock-master
	stock-supplier
	cre-master
	outer stock-notes
	system-table
	system-table2 = system-table
	name-and-address-master
where
	cr-type between uppercase (start-cre-type) 
	and uppercase (end-cre-type) 
	and cre-accountcode between uppercase (start-acc) 
	and uppercase (end-acc) 
	and stk-user-group[2] between uppercase (start-user-grp) 
	and uppercase (end-user-grp) 
	and stk-condition-code != 'O' 
	and stk-sort-analysis-code != spaces 
	and stock-note-type in { 'P' ' ' }
	and accountcode = cre-accountcode 
	and na-type = 'C' 
	and system-table.sys-tbl-type = "SX" 
	and system-table.sys-tbl-code = stk-sort-analysis-code 
	and system-table2.sys-tbl-type = "CQ" 
	and system-table2.sys-tbl-code = cr-def-payment-terms 
order by stock-code
format is rpt
sort on cr-type sort-analysis-money stk-sort-analysis-code stock-code 
field	ws-address-print	pic x
width is 80
report header
	set ws-address-print = NO
header
	print 
		"Cost Price List" in col 1
	skip 1
	print cr-shortname in col 1 bold
	skip 1
	if ws-address-print = YES
		print	"ADDRESS" in col 18
				na-name in col 26
		print	na-company in col 26
		print	na-street in col 26
		print	na-suburb in col 26
		print	na-country in col 26
		print	postcode in col 46
		print 	"PHONE" in col 18
				na-phone in col 26
		print 	"FAX" in col 18
				na-fax-no in col 26
		print	"TERMS" in col 18
				payment-terms-desc in col 26
		skip
	endif
	print
		"CODE" in col 1
		"DESCRIPTION" in col 18
		"SIZE" in col 49
		"COST" in col 57
		"+ TAX" in col 68
	skip 1
before ws-code-1-3
	set ws-address-print = YES
	page
	set ws-address-print = NO

before stk-sort-analysis-code
	need 4
	skip 2
	print sort-analysis-desc in col 18 bold underlined
	skip

before stock-code
	print
		stock-code in col 1
		stk-description in col 18
		stk-pack-desc in col 49
		ws-cost in col 54 pic z(6)9v99
		sup-last-buy-price in col 65 pic z(6)9v99
	if stk-desc-line-2 != spaces
		print
			stk-desc-line-2 col 18
	endif
before stock-note
	if stock-note != spaces
		print
			stock-note col 18
	endif

detail

footer
	print
		today() in col 43
		tod() in col 56
		"Pageno." in col 66
		pageno() in col 74
report footer
