select
	accountcode
	trans-type
	trans-date
	trans-ref
	tr-amount
	ws-start-date = input date "Start Date:"
	ws-end-date = input date "End Date:"
	tr-details
from
	deb-trans
where
	trans-date between ws-start-date and ws-end-date 
	and trans-type in { "CR" "CP" } 
	and substring (tr-details 1 2) not in { "CC" "DC" "AC" } 
order by trans-date trans-ref 
format is rpt
field total-tr-amount like tr-amount
report header
	page
	set total-tr-amount = 0
header
	print
		"DEBTORS CASH RECEIPTS/PAYMENTS" in col 11
		today() in col 55
		"Page No." in col 67
		pageno() in col 75
	skip
	skip
	print
		"  " in col 4
		"Order" in col 20
		"Customer" in col 32
		"Transaction" in col 46
	print
		"Date" in col 6
		"Number" in col 20
		"  Code" in col 32
		"   Type" in col 46
		"Amount" in col 66
	print "==============================================================================" in col 1
detail
	print
		trans-date in col 4
		trans-ref in col 18
		accountcode in col 33
		trans-type in col 50
		tr-amount in col 62
	set total-tr-amount += tr-amount
report footer
	print
		"---------------" in col 62
	print
		"Total Receipts/Payments:" in col 37 bold 
		total-tr-amount in col 62 bold 
skip 5
	print
		"Program Name:" in col 10
		"rjbcollect" in col 24 bold
	print
		"Start Date:" in col 10
		ws-start-date in col 24 bold
	print
		"End Date:" in col 10
		ws-end-date in col 24 bold
